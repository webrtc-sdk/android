buildscript {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.1.3"
        classpath 'com.github.kezong:fat-aar:1.3.8'
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

group = GROUP
version = VERSION_NAME

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://ossrh-staging-api.central.sonatype.com/service/local/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://central.sonatype.com/repository/maven-snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('nexusUsername') ? nexusUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('nexusPassword') ? nexusPassword : ""
}

nexusPublishing {
    packageGroup = GROUP
    repositories {
        maven {
            nexusUrl.set(uri(getReleaseRepositoryUrl()))
            snapshotRepositoryUrl.set(uri(getSnapshotRepositoryUrl()))
            username = getRepositoryUsername()
            password = getRepositoryPassword()
            stagingProfileId = STAGING_PROFILE_ID
        }
    }
}
